/*
 * LensKit, an open-source toolkit for recommender systems.
 * Copyright 2014-2017 LensKit contributors (see CONTRIBUTORS.md)
 * Copyright 2010-2014 Regents of the University of Minnesota
 *
 * Permission is hereby granted, free of charge, to any person obtaining
 * a copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */
import com.hierynomus.gradle.license.tasks.LicenseFormat

allprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        encoding 'UTF-8'
        header rootProject.file('LICENSE.md')
        useDefaultMappings false
        mapping('java': 'SLASHSTAR_STYLE', 'groovy': 'SLASHSTAR_STYLE', 'gradle': 'SLASHSTAR_STYLE')
    }

    // make license failures fail the build early
    project.tasks.withType(JavaCompile) { task ->
        switch (task.name) {
            case 'compile':
                task.mustRunAfter 'licenseMain'
                check.dependsOn 'licenseMain'
                break
            case 'compileTest':
                task.mustRunAfter 'licenseTest'
                check.dependsOn 'licenseTest'
                break
        }
    }
}

task licenseFormatGradle(type: LicenseFormat) {
    source = fileTree(projectDir) {
        include "*.gradle"
        include "gradle/*.gradle"
        include "buildSrc/*.gradle"
        include "lenskit-*/*.gradle"
        include "buildSrc/src/**/*.groovy"
        include "buildSrc/src/**/*.java"
    }
}
licenseFormat.dependsOn licenseFormatGradle