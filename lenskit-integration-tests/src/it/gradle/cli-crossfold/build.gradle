buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.hamcrest:hamcrest-library:1.3"
    }
}
apply plugin: 'java'


import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

apply from: 'common.gradle'

task crossfold(type: JavaExec) {
    dependsOn classes
    classpath configurations.runtime
    inputs.file 'predictor.groovy'
    inputs.file ratingsFile
    outputs.dir 'split'

    main 'org.lenskit.cli.Main'
    args 'crossfold'
    args "--tsv-file", ratingsFile
    args '--no-timestamps'
    args '-k', 10
    args '--gzip-output'
    args '-o', file('split')
}

check {
    dependsOn crossfold
    doLast {
        def dir = file('split')
        assertThat dir.exists(), equalTo(true)
        for (i in 1..10) {
            assertThat "train file $i should exist", file(String.format("split/part%02d.train.csv.gz", i)).exists()
            assertThat "train yaml file $i should exist", file(String.format("split/part%02d.train.yaml", i)).exists()
            assertThat "test file $i should exist", file(String.format("split/part%02d.test.csv.gz", i)).exists()
            assertThat "test yaml file $i should exist", file(String.format("split/part%02d.test.yaml", i)).exists()
        }
    }
}
