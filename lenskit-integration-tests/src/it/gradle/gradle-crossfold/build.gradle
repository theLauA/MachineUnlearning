buildscript {
    repositories {
        maven {
            url "$project.testRepoURI"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.hamcrest:hamcrest-library:1.3'
        classpath "org.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*
import static org.hamcrest.MatcherAssert.*
import static org.hamcrest.Matchers.*

apply plugin: 'java'
apply from: 'common.gradle'
apply plugin: 'lenskit'

task crossfold(type: Crossfold) {
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "gz"
    includeTimestamps false
}

check {
    dependsOn crossfold
    doLast {
        def dir = file("$buildDir/crossfold.out")
        assertThat dir.exists(), equalTo(true)
        assertThat "datasets file should exist", file("$dir/datasets.yaml").exists()
        for (int i = 1; i <= 5; i++) {
            assertThat "train file $i should exist", file(String.format("$dir/part%02d.train.csv.gz", i)).exists()
            assertThat "test file $i should exist", file(String.format("$dir/part%02d.test.csv.gz", i)).exists()
        }
    }
}
